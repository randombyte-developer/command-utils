buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2"
        }
    }
    dependencies {
        classpath "gradle.plugin.flavor.pie:promptsign:1.0.2"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.50"
    id "org.jetbrains.kotlin.kapt" version "1.2.50"
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

def isJitpack = System.getenv("JITPACK") != null

if (!isJitpack) {
    apply plugin: "flavor.pie.promptsign"
}

group "de.randombyte"
version "2.2.4"

repositories {
    jcenter()
    maven { url "https://repo.spongepowered.org/maven/" }
    maven { url "https://jitpack.io" }
    maven { url "http://repo.bstats.org/content/repositories/releases" }
}

configurations {
    compile.extendsFrom shadow
    compile.extendsFrom kapt
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.50"
    kapt "org.spongepowered:spongeapi:7.0.0"
    shadow("com.github.randombyte-developer.kosp:kosp:v2.2.3") { transitive = false }
    shadow "org.bstats:bstats-sponge:1.2"
}

jar.enabled = false

shadowJar {
    configurations = [project.configurations.shadow]

    relocate "kotlin", "de.randombyte.commandutils.shaded.kotlin"
    relocate "de.randombyte.kosp", "de.randombyte.commandutils.shaded.kosp"
    relocate "org.bstats", "de.randombyte.commandutils.shaded.bstats"

    classifier = null // Remove '-all' suffix from output file name
}
build.dependsOn shadowJar
if (!isJitpack) {
    signArchives.dependsOn shadowJar
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}